/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.example.jetcaster.buildsrc.Libs
import com.example.jetcaster.buildsrc.Versions
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '4.3.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()

        if (Libs.AndroidX.UI.version.endsWith("SNAPSHOT")) {
            maven {
                url "https://androidx.dev/snapshots/builds/${Libs.AndroidX.UI.snapshot}/artifacts/ui/repository/"
            }
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Don't require parens on fun type annotations e.g. `@Composable~()~ () -> Unit`
            // TODO: Remove when we move to Kotlin 1.4
            freeCompilerArgs += '-XXLanguage:+NonParenthesizedAnnotationsOnFunctionalTypes'

            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'

            // Set JVM target to 1.8
            jvmTarget = "1.8"

            // Compose is based on the Kotlin 1.4 compiler, but we need to use the 1.3.x Kotlin
            // library due to library compatibility, etc. Therefore we explicit set our apiVersion
            // to 1.3 to fix any warnings. Binary dependencies (such as Compose) can continue to
            // use 1.4 if built with that library.
            // TODO: remove this once we move to Kotlin 1.4
            apiVersion = "1.3"
        }
    }
}
