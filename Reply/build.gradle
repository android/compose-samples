/*
 * Copyright 2022 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.7.10'
    ext.compose_compiler_version = '1.3.0'
    ext.compose_compiler_snapshot_version = ''
    ext.compose_version = '1.2.1'
    ext.compose_snapshot_version = ''
    ext.coroutines_version = '1.6.0'
    ext.accompanist_version = '0.25.1'
    ext.material3_version = '1.0.0-alpha14'
    ext.agp_version = '7.2.2'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // Do not update until Ktlint 0.46.2 is released.
    // See https://github.com/pinterest/ktlint/pull/1525
    id 'com.diffplug.spotless' version '6.7.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()

        if (!compose_snapshot_version.isEmpty()) {
            maven { url "https://androidx.dev/snapshots/builds/" +
                    "${compose_snapshot_version}/artifacts/repository/" }
        }
        if (!compose_compiler_snapshot_version.isEmpty()) {
            maven { url "https://androidx.dev/snapshots/builds/" +
                    "${compose_compiler_snapshot_version}/artifacts/repository/" }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            // Do not update until 0.46.2 is released. With 0.46.1, we can't disable rules.
            // See https://github.com/pinterest/ktlint/pull/1525
            // 0.46 introduces a new filename check, that fails for our files. Once we upgrade to
            // 0.46.2, we should ignore that check using
            // .editorConfigOverride(["filename": "false"])
            ktlint("0.45.2")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}
