/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.4.30'
    ext.compose_version = '1.0.0-SNAPSHOT'
    ext.coroutines_version = '1.4.2'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha05'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

subprojects {
    repositories {
        maven {
            def snapshot = "7123292"
            url  "https://androidx.dev/snapshots/builds/$snapshot/artifacts/repository/"
        }
        google()
        jcenter()
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.39.0")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    // FIXME: Only needed for Compose 1.0.0-alpha12 snapshots
    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'androidx.lifecycle' &&
                    details.requested.name != 'lifecycle-viewmodel-compose') {
                details.useVersion('2.3.0-rc01')
            }
            if (details.requested.group == 'androidx.savedstate') {
                details.useVersion('1.1.0-rc01')
            }
        }
    }
}